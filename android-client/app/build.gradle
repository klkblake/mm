apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = '23.0.2'
        defaultConfig.with {
            applicationId = 'com.klkblake.mm'
            minSdkVersion.apiLevel = 21
            targetSdkVersion.apiLevel = 23
            versionCode = 1
            versionName = "1.0"
        }
    }

    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles += file('proguard-rules.pro')
        }
    }

    android.ndk {
        moduleName = "mm-crypto"
        // The includes are the same for all compiled versions
        CFlags += ['-std=c11', '-Wall', "-I${file("../../libsodium/libsodium-android-armv7-a/include")}".toString()]
        // The internet seems to indicate that our search path should automatically include these
        ldFlags += ['-Lsrc/main/jniLibs/armeabi-v7a', '-Lsrc/main/jniLibs/x86']
        ldLibs += 'sodium'
        abiFilters += ['armeabi-v7a', 'x86']
    }

    android.productFlavors {
        file('../../keys').listFiles().findAll { it.isDirectory() }.each({
            create(it.name)
        })
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile project(':common')
}

task buildContactLists(type: Exec) {
    commandLine './build'
    workingDir '../../keys'
}

tasks.getByPath(":app:preBuild").dependsOn.add(buildContactLists)